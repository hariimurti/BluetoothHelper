apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    def versionPropsFile = file('version.prop')
    Properties versionProps = new Properties()
    if(versionPropsFile.exists())
        versionProps.load(new FileInputStream(versionPropsFile))

    def _versionCode = (versionProps['VERSION_CODE'] ?: "1").toInteger()
    def _major = (versionProps['VERSION_MAJOR'] ?: "1").toInteger()
    def _minor = (versionProps['VERSION_MINOR'] ?: "0").toInteger()
    def _patch = (versionProps['PATCH'] ?: "0").toInteger()

    List<String> runTasks = gradle.startParameter.getTaskNames()
    for (String item : runTasks) {
        if (item.contains("assembleDebug") || item.contains("assembleRelease"))
            _versionCode = _versionCode + 1
        if (item.contains("assembleDebug"))
            _patch = _patch + 1
        if (item.contains("assembleRelease")) {
            _minor = _minor + 1
            _patch = 0
        }
    }

    if(_minor == 10){
        _minor = 0
        _major = _major + 1
    }

    versionProps['VERSION_CODE'] = _versionCode.toString()
    versionProps['VERSION_MAJOR'] = _major.toString()
    versionProps['VERSION_MINOR'] = _minor.toString()
    versionProps['PATCH'] = _patch.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
        applicationId "net.harimurti.bluetooth"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode _versionCode
        versionName "${_major}.${_minor}.${_patch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}.apk"
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //noinspection GradleCompatible
    implementation 'com.android.support:percent:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
}
